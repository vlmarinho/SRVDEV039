



/* 
--------------------------------------------------------------------------
NOME SISTEMA: AUTORIZAÇÃO
OBJETO: [DBO].[PR_AUT_RELATORIOFECHAMENTO]
PROPÓSITO: PROCEDURE RESPONSÁVEL PELO RELATORIO DE FECHAMENTO DO POS WALK
AUTOR: IURI LINO - TECNOLOGIA POLICARD
--------------------------------------------------------------------------
DATA CRIAÇÃO: 08/08/2011
SA: 519773
--------------------------------------------------------------------------
DATA ALTERAÇÃO: 28/12/2012
SA: 16609
--------------------------------------------------------------------------
Data Alteração: 09/12/2015
Mudança: 232270 -- 1448 
Autor: Cristiano Barbosa
--------------------------------------------------------------------------
Data Alteração: 19/04/2016
Mudança: 261309 -- 1809 
Autor: Cristiano Barbosa
--------------------------------------------------------------------------
*/

CREATE PROCEDURE [pr_aut_RelatorioFechamentoPOS] (
	 @cTERMINAL					VARCHAR(1000)
	,@iCodigoEstabelecimento	VARCHAR(1000)
	,@bEstabMigrado				BIT
	,@cDATA  					VARCHAR(1000)
	,@cRESPOSTA					VARCHAR(1000) OUTPUT
	,@cTICKET					VARCHAR(1000) OUTPUT
)
AS
BEGIN

	DECLARE
		 @sCNPJ_ESTABELECIMENTO			VARCHAR(18)
		,@sNOME_ESTABELECIMENTO			VARCHAR(41)
		,@sENDERECO_ESTABELECIMENTO		VARCHAR(41)
		,@sCIDADE_ESTABELECIMENTO		VARCHAR(30)
		,@iQTDE_FROTA					INT
		,@iQTDE_ESTORNO_FROTA			INT
		,@IESTCODIGO					INT
		,@iQTDE_CONVENIO				INT
		,@iQTDE_ESTORNO_CONVENIO		INT
		,@iQTDE_TROCO					INT
		,@iQTDE_ESTORNO_TROCO			INT
		,@iQTDE_BOMBA_INTERNA			INT
		,@iQTDE_ESTORNO_BOMBA_INTERNA	INT
		,@iQTDE_CONVENIO_FINAN			INT
		,@iQTDE_ESTORNO_CONVENIO_FINAN	INT
		,@iQTDE_PROCESSADORA  			INT
		,@iQTDE_ESTORNO_PROCESSADORA    INT
		,@iQTDE_ESTORNO_AUTORIZACAO		INT
		,@iQTDE_AUTORIZACAO				INT
		,@iQTDE_TOTAL_RELATORIO		    INT
		,@iQTDE_ESTORNO_REL	    	    INT
		,@sESTADO_ESTABELECIMENTO		CHAR(2)
		,@sSTATUS_ESTABELECIMENTO		CHAR(1)
		,@dVALOR_CONVENIO				DECIMAL(15,2)
		,@dVALOR_ESTORNO_CONVENIO		DECIMAL(15,2)
		,@dVALOR_TROCO					DECIMAL(15,2)
		,@dVALOR_ESTORNO_TROCO			DECIMAL(15,2)
		,@dVALOR_BOMBA_INTERNA			DECIMAL(15,2)
		,@dVALOR_ESTORNO_BOMBA_INTERNA	DECIMAL(15,2)
		,@dVALOR_CONVENIO_FINAN			DECIMAL(15,2)
		,@dVALOR_ESTORNO_CONVENIO_FINAN	DECIMAL(15,2)
		,@dVALOR_FROTA					DECIMAL(15,2)
		,@dVALOR_ESTORNO_FROTA			DECIMAL(15,2)
		,@dVALOR_PROCESSADORA 			DECIMAL(15,2)
		,@dVALOR_ESTORNO_PROCESSADORA	DECIMAL(15,2)
		,@dVALOR_AUTORIZACAO			DECIMAL(15,2)	
		,@dVALOR_ESTORNO_AUTORIZACAO	DECIMAL(15,2)	
		,@dVALOR_TOTAL_RELATORIO		DECIMAL(15,2)
		,@dVALOR_ESTORNO_REL  			DECIMAL(15,2)
		,@dDATA						    DATETIME
		,@cDATASTRING					CHAR(10)
		,@iRESPOSTA						INT
	
	SET @iResposta = 0
	SET @cDATASTRING = CONVERT(VARCHAR,GETDATE(),112)
	
	IF (ISDATE(@cDATA) = 0 OR (@dDATA > @cDATASTRING))
		SET @iResposta = 144

	IF (@iResposta = 0)
	BEGIN       
		
		IF (@bEstabMigrado = 0)
		BEGIN
			SELECT 
				 @sCNPJ_ESTABELECIMENTO      = CNPJ
				,@sNOME_ESTABELECIMENTO      = LTRIM(RTRIM(NOME))
				,@sENDERECO_ESTABELECIMENTO  = LTRIM(RTRIM(ENDERECO))
				,@sCIDADE_ESTABELECIMENTO    = LTRIM(RTRIM(CIDADE))
				,@sESTADO_ESTABELECIMENTO    = ESTADO
				,@iESTCODIGO				 = ESTCODIGO
				,@sSTATUS_ESTABELECIMENTO	 = STATUS
			FROM PROCESSADORA.dbo.ESTABELECIMENTOS WITH(NOLOCK)
			WHERE NUMERO  = @iCodigoEstabelecimento /* PARÂMETRO DE ENTRADA REFERENTE AO CÓDIGO POLICARD DO ESTABELECIMENTO */     
		
			IF @iESTCODIGO IS NULL      
				SET @iResposta = 116 /* ESTABELECIMENTO NAO ENCONTRADO - ESTAB. INVAL.-03*/
		END
		ELSE
		BEGIN

			SELECT 
				 @sSTATUS_ESTABELECIMENTO	= CASE WHEN E.CodigoEntidadeStatus = 1 THEN 'A'
												   WHEN E.CodigoEntidadeStatus = 2 THEN 'I'
												   WHEN E.CodigoEntidadeStatus = 3 THEN 'C'
												   WHEN E.CodigoEntidadeStatus = 4 THEN 'P'
												   WHEN E.CodigoEntidadeStatus = 6 THEN 'B'
											  END
				,@sCNPJ_ESTABELECIMENTO		= dbo.f_FormatarCNPJ_CPF(E.Cnpj)
				,@sNOME_ESTABELECIMENTO		= LTRIM(RTRIM(E.Nome))
				,@sEndereco_Estabelecimento	= LTRIM(RTRIM(EE.Logradouro))
				,@sCidade_Estabelecimento	= LTRIM(RTRIM(EE.Cidade))
				,@sEstado_Estabelecimento	= EE.UF
			FROM Acquirer.dbo.Estabelecimento E WITH (NOLOCK)
			INNER JOIN Acquirer.dbo.EstabelecimentoEndereco EE WITH (NOLOCK) ON E.CodigoEstabelecimento = EE.CodigoEstabelecimento
			WHERE E.CodigoEstabelecimento = @iCodigoEstabelecimento
			AND EE.CodigoTipoEndereco = 1



		END
		
		IF @sSTATUS_ESTABELECIMENTO <> 'A' /* ESTABELECIMENTO BLOQUEADO/CANCELADO*/
			SET @iRESPOSTA = 320
	
	END
	
	IF @iRESPOSTA = 0
	BEGIN 
		
		/* QUANTIDADE E VALORES DE TRANSACAOES CONVENIO E FROTA */
		SET @dDATA = CONVERT(DATETIME, SUBSTRING (@cDATA, 1 ,4) + '-' + SUBSTRING (@cDATA, 5 ,2) + '-' + SUBSTRING (@cDATA , 7 ,2))
		
		DECLARE @VTable TABLE ( TIPO_MENSAGEM			VARCHAR (4)
							   ,CODIGO_PROCESSAMENTO	VARCHAR (6)
							   ,VALOR_OPERACAO			DECIMAL (15,2)
							   ,VALOR_TROCO				DECIMAL (15,2)
							   ,ESTORNO					INT
							   ,OPCAOFROTA				TINYINT
							   )

		INSERT INTO @VTable
		SELECT 
			 TE.TIPO_MENSAGEM
			,TE.CODIGO_PROCESSAMENTO
			,TE.VALOR_OPERACAO
			,TT.VALOR_SOLICITADO
			,TE.ESTORNO
			,TE.OPCAOFROTA
		FROM  
			  POLICARD_603078.DBO.TRANSACAO_ELETRONICA TE WITH(NOLOCK)
			LEFT JOIN   POLICARD_603078.DBO.TRANSACAO_TROCO TT WITH (NOLOCK) ON TT.COD_TRANSACAOFINANCIAMENTO = TE.CODIGO
		WHERE
			TE.ID_REDE IN (13,58)
		AND TE.TERMINAL = @cTERMINAL
		AND TE.DATA_AUTORIZACAO BETWEEN @dDATA AND DATEADD(DAY, 1, @dDATA)

		
		SELECT
			 @dVALOR_CONVENIO			= COALESCE(SUM(CASE WHEN VT.TIPO_MENSAGEM = '0200' AND VT.OPCAOFROTA IS NULL THEN VT.VALOR_OPERACAO ELSE 0 END), 0)
			,@iQTDE_CONVENIO			= COALESCE(SUM(CASE WHEN VT.TIPO_MENSAGEM = '0200' AND VT.OPCAOFROTA IS NULL THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_CONVENIO	= COALESCE(SUM(CASE WHEN VT.TIPO_MENSAGEM = '0400' AND VT.OPCAOFROTA IS NULL THEN VT.VALOR_OPERACAO ELSE 0 END),0)
			,@iQTDE_ESTORNO_CONVENIO	= COALESCE(SUM(CASE WHEN VT.TIPO_MENSAGEM = '0400' AND VT.OPCAOFROTA IS NULL THEN 1 ELSE 0 END),0)
			,@dVALOR_FROTA				= COALESCE(SUM(CASE WHEN VT.ESTORNO IS NULL  AND VT.OPCAOFROTA IS NOT NULL THEN VT.VALOR_OPERACAO ELSE 0 END),0)
			,@iQTDE_FROTA				= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO > 0  AND VT.OPCAOFROTA IS NOT NULL THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_FROTA		= COALESCE(SUM(CASE WHEN VT.ESTORNO IS NOT NULL AND VT.OPCAOFROTA IS NOT NULL THEN VT.VALOR_OPERACAO ELSE 0 END),0)
			,@iQTDE_ESTORNO_FROTA		= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO < 0 AND VT.OPCAOFROTA IS NOT NULL THEN 1 ELSE 0 END),0)
			,@dVALOR_TROCO				= COALESCE(SUM(CASE WHEN VT.CODIGO_PROCESSAMENTO = '140000' AND VT.TIPO_MENSAGEM = '0200' THEN VT.VALOR_TROCO ELSE 0 END), 0)
			,@iQTDE_TROCO				= COALESCE(SUM(CASE WHEN VT.CODIGO_PROCESSAMENTO = '140000' AND VT.TIPO_MENSAGEM = '0200' THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_TROCO		= COALESCE(SUM(CASE WHEN VT.CODIGO_PROCESSAMENTO = '140000' AND VT.ESTORNO IS NOT NULL THEN VT.VALOR_TROCO * (-1) ELSE 0 END),0)
			,@iQTDE_ESTORNO_TROCO		= COALESCE(SUM(CASE WHEN VT.CODIGO_PROCESSAMENTO = '140000' AND VT.TIPO_MENSAGEM = '0400' THEN 1 ELSE 0 END),0)
		FROM 
			@VTable VT
		
		DELETE FROM @VTable

		INSERT INTO @VTable
		SELECT 
			 TE.TIPO_MENSAGEM
			,TE.CODIGO_PROCESSAMENTO
			,TE.VALOR_OPERACAO
			,0
			,TE.ESTORNO
			,TE.OPCAOFROTA
		FROM  
			  POLICARD_603078.DBO.TRANSACAO_ELETRONICA_BOMBAINTERNA TE WITH(NOLOCK)
		WHERE
			TE.ID_REDE IN (13,58)
			AND TE.TERMINAL = @cTERMINAL
			AND TE.DATA_AUTORIZACAO BETWEEN @dDATA AND DATEADD(DAY, 1, @dDATA)
			
		SELECT 
			 @dVALOR_BOMBA_INTERNA			= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO > 0 AND VT.ESTORNO IS NULL THEN VT.VALOR_OPERACAO ELSE 0 END), 0)
			,@iQTDE_BOMBA_INTERNA			= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO > 0 AND VT.ESTORNO IS NULL THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_BOMBA_INTERNA	= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO < 0 AND VT.ESTORNO IS NOT NULL THEN VT.VALOR_OPERACAO ELSE 0 END),0)
			,@iQTDE_ESTORNO_BOMBA_INTERNA	= COALESCE(SUM(CASE WHEN VT.VALOR_OPERACAO < 0 AND VT.ESTORNO IS NOT NULL THEN 1 ELSE 0 END),0)
		FROM 
			@VTable VT

		DELETE FROM @VTable

		/*TRANSACOES FINANCIAMENTO*/
		SELECT 
			 @dVALOR_CONVENIO_FINAN			= COALESCE(SUM(CASE WHEN tipo_mensagem = '0200' THEN VALOR ELSE 0 END), 0)
			,@iQTDE_CONVENIO_FINAN			= COALESCE(SUM(CASE WHEN tipo_mensagem = '0200' AND VALOR > 0 THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_CONVENIO_FINAN = COALESCE(SUM(CASE WHEN tipo_mensagem = '0400' THEN VALOR * (-1) ELSE 0 END),0)
			,@iQTDE_ESTORNO_CONVENIO_FINAN	= COALESCE(SUM(CASE WHEN tipo_mensagem = '0400' AND VALOR > 0 THEN 1 ELSE 0 END),0)
		FROM
			  POLICARD_603078.DBO.Transacao_RegistroTEF WITH (NOLOCK) 
		WHERE 
			TERMINAL = @cTERMINAL
			AND TABELA = 'F'
			AND StatusTef <> 'D'
			AND DataAutorizacao BETWEEN @dDATA AND DATEADD(DAY, 1, @dDATA)

		/*SOMANDO TRANSACOES A VISTA + PARCELADAS*/
		SET @dVALOR_CONVENIO			= @dVALOR_CONVENIO + @dVALOR_CONVENIO_FINAN + @dVALOR_TROCO
		SET @iQTDE_CONVENIO				= @iQTDE_CONVENIO + @iQTDE_CONVENIO_FINAN --+ @iQTDE_TROCO	
		SET @dVALOR_ESTORNO_CONVENIO	= @dVALOR_ESTORNO_CONVENIO + @dVALOR_ESTORNO_CONVENIO_FINAN + @dVALOR_ESTORNO_TROCO
		SET @iQTDE_ESTORNO_CONVENIO		= @iQTDE_ESTORNO_CONVENIO + @iQTDE_ESTORNO_CONVENIO_FINAN --+ @iQTDE_ESTORNO_TROCO

		/*SOMANDO TRANSACOES FROTA + BOMBA INTERNA*/
		SET @dVALOR_FROTA			= @dVALOR_FROTA	+ @dVALOR_BOMBA_INTERNA	
		SET @iQTDE_FROTA			= @iQTDE_FROTA + @iQTDE_BOMBA_INTERNA	
		SET @dVALOR_ESTORNO_FROTA	= @dVALOR_ESTORNO_FROTA + @dVALOR_ESTORNO_BOMBA_INTERNA
		SET @iQTDE_ESTORNO_FROTA	= @iQTDE_ESTORNO_FROTA + @iQTDE_ESTORNO_BOMBA_INTERNA
		
	END
	

	IF @iRESPOSTA = 0
	BEGIN

		SET @dDATA = CONVERT(DATETIME, SUBSTRING (@cDATA, 1 ,4) + '-' + SUBSTRING (@cDATA, 5 ,2) + '-' + SUBSTRING (@cDATA , 7 ,2))
						
		SELECT
			 @dVALOR_AUTORIZACAO		 = COALESCE(SUM(CASE WHEN T.STATUS = 'A' THEN T.VALOR ELSE 0 END),0)
			,@iQTDE_AUTORIZACAO			 = COALESCE(SUM(CASE WHEN T.STATUS = 'A' THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_AUTORIZACAO = COALESCE(SUM(CASE WHEN T.STATUS = 'E' THEN T.VALOR * (-1) ELSE 0 END),0)
			,@iQTDE_ESTORNO_AUTORIZACAO	 = COALESCE(SUM(CASE WHEN T.STATUS = 'E' THEN 1 ELSE 0 END),0)
		FROM 
			Autorizacao.dbo.Transacoes T WITH (NOLOCK)
		INNER JOIN PROCESSADORA.dbo.TIPOSPRODUTOS P WITH(NOLOCK) ON T.TPOPRDCODIGO = P.TPOPRDCODIGO
		INNER JOIN PROCESSADORA.dbo.MEIOSCAPTURA MC WITH(NOLOCK) ON T.MEICPTCODIGO = MC.MEICPTCODIGO
		WHERE
			MC.NUMERO = @cTERMINAL
			AND T.DATA BETWEEN @dDATA AND DATEADD(DAY, 1, @dDATA)
		
		SELECT 
			 @dVALOR_PROCESSADORA			= COALESCE(SUM(CASE WHEN LT.STATUS = 'A' THEN LT.VALOR ELSE 0 END),0)
			,@iQTDE_PROCESSADORA			= COALESCE(SUM(CASE WHEN LT.STATUS = 'A' THEN 1 ELSE 0 END),0)
			,@dVALOR_ESTORNO_PROCESSADORA	= COALESCE(SUM(CASE WHEN LT.STATUS = 'E' THEN LT.VALOR * (-1) ELSE 0 END),0)
			,@iQTDE_ESTORNO_PROCESSADORA	= COALESCE(SUM(CASE WHEN LT.STATUS = 'E' THEN 1 ELSE 0 END),0)
		FROM 
			PROCESSADORA.dbo.LOGTRANSACOES LT WITH (NOLOCK) 
			INNER JOIN PROCESSADORA.dbo.TIPOSPRODUTOS P WITH (NOLOCK) ON LT.TPOPRDCODIGO = P.TPOPRDCODIGO
			INNER JOIN PROCESSADORA.dbo.MEIOSCAPTURA MC WITH (NOLOCK) ON LT.MEICPTCODIGO = MC.MEICPTCODIGO   
		WHERE 
				LT.RDECODIGO IN (8,12)
			AND LT.ESTCODIGO = @iESTCODIGO
			AND	LT.TERMINAL = @cTERMINAL
			AND MC.TPOMEICPTCODIGO IN (11, 14)
			AND LT.DATA BETWEEN @dDATA AND DATEADD(DAY, 1, @dDATA)

		/*SOMANDO TRANSACOES DA AUTORIZACAO + PROCESSADORA*/
		SET @dVALOR_PROCESSADORA		 = @dVALOR_PROCESSADORA + @dVALOR_AUTORIZACAO
		SET @iQTDE_PROCESSADORA			 = @iQTDE_PROCESSADORA + @iQTDE_AUTORIZACAO	
		SET @dVALOR_ESTORNO_PROCESSADORA = @dVALOR_ESTORNO_PROCESSADORA + @dVALOR_ESTORNO_AUTORIZACAO
		SET @iQTDE_ESTORNO_PROCESSADORA	 = @iQTDE_ESTORNO_PROCESSADORA + @iQTDE_ESTORNO_AUTORIZACAO

	END
		
	IF @iRESPOSTA = 0
	BEGIN
		SET @cRESPOSTA = '00'

		SELECT @cTICKET = WALK_MSGTICKET FROM  Autorizacao.dbo.AUT_MENSAGENSRESPOSTA WITH (NOLOCK) WHERE TPOPRDCODIGO = 0 AND TPOTRNCODIGO = 0 AND REDE in (13,58)
	
		SET @dVALOR_TOTAL_RELATORIO = @dVALOR_CONVENIO + @dVALOR_FROTA + @dVALOR_PROCESSADORA
		SET @iQTDE_TOTAL_RELATORIO	= CONVERT (INT,@iQTDE_CONVENIO) + CONVERT (INT,@iQTDE_FROTA) + CONVERT (INT,@iQTDE_PROCESSADORA)
		SET @dVALOR_ESTORNO_REL		= @dVALOR_ESTORNO_CONVENIO + @dVALOR_ESTORNO_FROTA + @dVALOR_ESTORNO_PROCESSADORA
		SET @iQTDE_ESTORNO_REL		= CONVERT (INT,@iQTDE_ESTORNO_CONVENIO) + CONVERT (INT,@iQTDE_ESTORNO_FROTA) + CONVERT (INT,@iQTDE_ESTORNO_PROCESSADORA)
			
		
		SET @cTICKET = REPLACE(@cTICKET,'<DATA_RELATORIO>',SUBSTRING (@cDATA , 7 ,2) + '/' + SUBSTRING (@cDATA, 5 ,2) + '/' + SUBSTRING (@cDATA, 1 ,4))
		SET @cTICKET = REPLACE(@cTICKET,'<CNPJ>',@sCNPJ_ESTABELECIMENTO)
		SET @cTICKET = REPLACE(@cTICKET,'<NOME_ESTABELECIMENTO>',@sNOME_ESTABELECIMENTO)
		SET @cTICKET = REPLACE(@cTICKET,'<ENDERECO>',@sENDERECO_ESTABELECIMENTO)
		SET @cTICKET = REPLACE(@cTICKET,'<CIDADE>',@sCIDADE_ESTABELECIMENTO)
		SET @cTICKET = REPLACE(@cTICKET,'<ESTADO>',@sESTADO_ESTABELECIMENTO)
		SET @cTICKET = REPLACE(@cTICKET,'<CODIGO_ESTABELECIMENTO>',CONVERT(INT, @iCodigoEstabelecimento))
		SET @cTICKET = REPLACE(@cTICKET,'<TERMINAL>',@cTERMINAL)
		SET @cTICKET = REPLACE(@cTICKET,'<DATA>',CONVERT(VARCHAR,GETDATE(),103))
		SET @cTICKET = REPLACE(@cTICKET,'<HORA>',CONVERT(VARCHAR,GETDATE(),108))
		SET @cTICKET = REPLACE(@cTICKET,'<VLR_TOTAL_REL>',COALESCE(@dVALOR_TOTAL_RELATORIO,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_TOTAL_REL>',COALESCE(@iQTDE_TOTAL_RELATORIO,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VLR_ESTORNO_REL>',COALESCE(@dVALOR_ESTORNO_REL,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_ESTORNO_REL>',COALESCE(@iQTDE_ESTORNO_REL,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_CONVENIO>',COALESCE(@iQTDE_CONVENIO,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_CONVENIO>',COALESCE(@dVALOR_CONVENIO,0)) 
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_ESTORNO_CONVENIO>',COALESCE(@iQTDE_ESTORNO_CONVENIO,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_ESTORNO_CONVENIO>',COALESCE(@dVALOR_ESTORNO_CONVENIO,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_FROTA>',COALESCE(@iQTDE_FROTA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_FROTA>',COALESCE(@dVALOR_FROTA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_ESTORNO_FROTA>',COALESCE(@iQTDE_ESTORNO_FROTA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_ESTORNO_FROTA>',COALESCE(@dVALOR_ESTORNO_FROTA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_PROCESSADORA>',COALESCE(@iQTDE_PROCESSADORA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_PROCESSADORA>',COALESCE(@dVALOR_PROCESSADORA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<QTDE_ESTORNO_PROCESSADORA>',COALESCE(@iQTDE_ESTORNO_PROCESSADORA,0))
		SET @cTICKET = REPLACE(@cTICKET,'<VALOR_ESTORNO_PROCESSADORA>',COALESCE(@dVALOR_ESTORNO_PROCESSADORA,0))

	END
	ELSE
	BEGIN
		EXEC PR_AUT_CONSULTARRESPOSTAPOLICARD @iRESPOSTA, 58, @cRESPOSTA OUTPUT, @cTICKET OUTPUT
	END
END





